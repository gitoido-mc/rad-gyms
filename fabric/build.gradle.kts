/*
 * Copyright (c) 2025-2026. gitoido-mc
 * This Source Code Form is subject to the terms of the GNU General Public License v3.0.
 * If a copy of the GNU General Public License v3.0 was not distributed with this file,
 * you can obtain one at https://github.com/gitoido-mc/rad-gyms/blob/main/LICENSE.
 */
@file:Suppress("MaxLineLength")

plugins {
    id("dev.architectury.loom")
    id("architectury-plugin")
    id("com.gradleup.shadow")
}

repositories {
    maven("https://maven.fabricmc.net/")
}

val generatedResources: File = project(":common").file("src/generated")!!

architectury {
    platformSetupLoomIde()
    fabric {
        fabricApi {
            configureDataGeneration {
                client = true
                modId = rootProject.property("mod_id") as String
                outputDirectory = generatedResources
            }
        }
    }
}

val shadowCommon: Configuration by configurations.creating {
    isCanBeResolved = true
    isCanBeConsumed = false
}

loom {
    silentMojangMappingsLicense()
    enableTransitiveAccessWideners.set(true)

    runs {
        getByName("client") {
            programArgs(
                "--username=Gitoido",
                "--uuid=23131d78-9edb-48a4-902a-e22e572e9f2b",
            )
        }
    }
}

dependencies {
    minecraft("com.mojang:minecraft:${property("minecraft_version")}")
    mappings(loom.officialMojangMappings())

    // Fabric
    modImplementation("net.fabricmc:fabric-loader:${property("fabric_loader_version")}")
    modImplementation("net.fabricmc.fabric-api:fabric-api:${property("fabric_version")}")
    modImplementation("net.fabricmc:fabric-language-kotlin:${property("fabric_kotlin_version")}")
    modImplementation("dev.architectury:architectury-fabric:${property("architectury_api_version")}")
    if (!property("use_cobbled_snapshot").toString().toBooleanStrict()) {
        modImplementation("com.cobblemon:fabric:${property("cobblemon_version")}+${property("minecraft_version")}") {
            isTransitive = false
        }
    } else {
        modImplementation("com.cobblemon:fabric:${property("cobblemon_snapshot_version")}+${property("minecraft_version")}-SNAPSHOT") {
            isTransitive = false
        }
    }

    // Common code
    implementation(project(":common", configuration = "namedElements"))
    "developmentFabric"(project(":common", configuration = "namedElements"))
    shadowCommon(project(":common", configuration = "transformProductionFabric"))

    modImplementation("curse.maven:radical-cobblemon-trainers-api-1152792:${property("rctapi_fabric_version")}")
    modCompileOnly("com.aetherteam.aether:aether:${property("aether_version")}-fabric")
}

tasks {
    val copyAccessWidener by registering(Copy::class) {
        from(project(":common").file("src/main/resources/rad_gyms.accesswidener"))
        into(file("src/main/resources").absolutePath)
    }

    val cleanupGenerated by registering(Delete::class) {
        delete(file("src/generated"))
    }

    val copyGenerated by registering(Copy::class) {
        dependsOn(cleanupGenerated)
        mustRunAfter(cleanupGenerated)
        from(project(":common").file("src/generated")) {
            exclude(".cache/")
        }
        into(file("src/generated"))
    }

    processResources {
        dependsOn(copyAccessWidener)
        dependsOn(copyGenerated)

        inputs.property("version", project.version)

        filesMatching("fabric.mod.json") {
            expand(project.properties)
        }
    }

    sourcesJar {
        dependsOn(copyAccessWidener)
    }

    shadowJar {
        configurations = listOf(shadowCommon)

        archiveBaseName.set("${rootProject.name}-${project.name}")
        archiveVersion.set("${project.version}")
        archiveClassifier.set("shadow")
    }

    remapJar {
        injectAccessWidener = true
        dependsOn(shadowJar)
        inputFile.set(shadowJar.flatMap { it.archiveFile })

        archiveBaseName.set("${rootProject.name}-${project.name}")
        archiveVersion.set("${project.version}")
    }

    remapSourcesJar {
        archiveBaseName.set("${rootProject.name}-${project.name}")
        archiveVersion.set("${project.version}")
        archiveClassifier.set("sources")
    }
}
